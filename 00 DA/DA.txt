/*
	Name: DA.txt
	Copyright: ALL Right Reserved 2018
	Authors: Dmitry Bychkov
		Artem Zaitsev
	Date: 19.12.18 20:17
	Description:
		cyntax part of programming language DA
*/
ТЗ - DA

Синтаксис DA:
	<программа> ::= <тег>[<программа>]|<объявление>[<программа>]
	%<строка> ::= “{<символ>}”
	%<integer> ::= <цифра>|<цифра><integer>
	%<float> ::= <integer><точка>|<integer><точка><integer>
	%<bool> ::= true|false
	%<точка> ::= .
	%<цифра> ::= 0|1|2|3|4|5|6|7|8|9
	%<имя> ::= #<п.имя>|<буква><п.имя>
	%<п.имя> ::= <цифра>[<п.имя>]|<буква>[<п.имя>]|<ничего>
	%<число> ::= <integer>|<float>
	%<ничего> ::=
	%<терминал> ::= <\n>|;
	#<тег> ::= <#in>|<#start>|<#ifdef>|<#def>|<#endef>|<#ifndef>
	#<#in> ::= #in <строка><\n>
	#<#start> ::= #start<оператор><опц. раздел.>
	#<#ifdef> ::= #ifdef <имя><\n><программа><\n>#endif<\n>
	#<#def> ::= #def <имя> <символы><\n>
	#<#endef> ::= #endef <имя><\n>
	#<#ifndef> ::= #ifndef <имя><\n><программа><\n>#endif<\n>
	<объявление> ::= <объявление переменной>|<объявление функции>
	<объявление переменной> ::= <имя типа><список имён><терминал>
	<список имён> ::= <имя>[(<оператор>)]{,<имя>[(<оператор>)]}
	<имя типа> ::= <имя><спецификатор>
	<спецификатор> ::= <*>|<ничего>
	<объявление функции> ::=
		<имя> ::= <описание типов><описание имён><описание ветвления>
	<описание типов> ::= [<имя типа>{,<имя типа>}][->[<имя типа>]]
	<описание имён> ::= ([<имя>{,<имя>}])
	<описание ветвления> ::=
		<оператор>{<опц. раздел.>([<оператор>{,<оператор>}])<оператор>}<опц. раздел.>
	<опц. раздел.> ::= <ничего>|<терминал>
	<оператор> ::= <if>|<while>|<for>|<выражение><терминал>|<блок>|<switch>
	<switch> ::= switch(<оператор>)<{>{case <cases>: <оператор><терминал>}<}>
	<блок> ::= <{><оператор>{<терминал><оператор>}<}>[-><имя типа>]
	<for> ::= for(<тело for>)<оператор>[else <оператор>]
	<тело for> ::= <выражение><терминал><выражение><терминал><выражение>
	<while> ::= while(<выражение>) <оператор> [else <оператор>]
	<if> ::= if(<выражение>) <оператор> [else <оператор>]
	<выражение> ::= <exp 15>|<имя>:|goto <имя>|<::=><оператор>
	<exp 15> ::= <exp 14>{<приоритет 15><exp 14>}
	<exp 14> ::= <exp 13>{<приоритет 14><exp 13>}
	<exp 13> ::= <exp 12>{<приоритет 13><exp 12>}
	<exp 12> ::= <exp 11>{<приоритет 12><exp 11>}
	<exp 11> ::= <exp 10>{<приоритет 11><exp 10>}
	<exp 10> ::= <exp 9>{<приоритет 10><exp 9>}
	<exp 9> ::= <exp 8>{<приоритет 9><exp 8>}
	<exp 8> ::= <exp 7>{<приоритет 8><exp 7>}
	<exp 7> ::= <exp 6>{<приоритет 7><exp 6>}
	<exp 6> ::= <exp 5>{<приоритет 6><exp 5>}
	<exp 5> ::= <exp 4>{<приоритет 5><exp 4>}
	<exp 4> ::= <exp 3>{<приоритет 4><exp 3>}
	<exp 3> ::= {<приоритет 2>}<exp 2>
	<exp 2> ::= {<приоритет 1>}<exp 1>
	<exp 1> ::= (<exp 15>)|<имя>|<строка>|<число>|sizeof(<имя типа>)|asm(<строка>)|<::><имя>
	<приоритет 1> ::= <@>|<$>|<*>|<&>|<++>|<-->
	<приоритет 2> ::= <!>|<~>|<+>|<->|(<имя типа>)
	<приоритет 3> ::= <**>
	<приоритет 4> ::=  <*>|<%>|</>
	<приоритет 5> ::= <+>|<->
	<приоритет 6> ::= <>>>|<<<>
	<приоритет 7> ::= <^>
	<приоритет 8> ::= <&>
	<приоритет 9> ::= <|>
	<приоритет 10> ::= <<=>|<<>|<>>|<>=>
	<приоритет 11> ::= <==>|<!=>
	<приоритет 12> ::= <&&>
	<приоритет 13> ::= <||>
	<приоритет 14> ::= <=*>|<=->|<=+>|<=%>|<=/>|<=<<>|<=>>>|<=&>|<=|>|<=^>|<=>>
	<приоритет 15> ::= <,>

	Типы: int, char, float, bool

	{ } - Повторить 0 или более раз
	[ ] - повторить 0 или 1 раз
	# - препроцессинг
	% - преобразование на лексическом уровне

Синтаксис llbw:
	in progress...
